// Copyright 2014-2017 Oxford University Innovation Limited and the authors of InfiniTAM

#include "MultiMeshingEngine_CUDA.h"

#include "../../../../ORUtils/CUDADefines.h"
#include "../../../Utils/CUDAUtils.h"

#include "../Shared/MeshingEngine_Shared.h"
#include "../Shared/MultiMeshingEngine_Shared.h"

using namespace ITMLib;

template<class TMultiVoxel, class TMultiIndex>
__global__ void
meshScene_device(Mesh::Triangle* triangles, unsigned int* noTriangles_device, float factor,
                 const unsigned int voxelBlockCount, int noMaxTriangles, const Vector4s* visibleBlockGlobalPos,
                 const TMultiVoxel* localVBAs, const TMultiIndex* hashTables);

template<class TMultiIndex>
__global__ void findAllocateBlocks(Vector4s* visibleBlockGlobalPos, const TMultiIndex* hashTables,
                                   const int noTotalEntries, const unsigned int hashEntryCount);

template<class TVoxel>
MultiMeshingEngine_CUDA<TVoxel, VoxelBlockHash>::MultiMeshingEngine_CUDA(const VoxelBlockHash& index) {
	ORcudaSafeCall(cudaMalloc((void**) &visibleBlockGlobalPos_device,
	                          index.voxel_block_count * sizeof(Vector4s) * MAX_NUM_LOCALMAPS));
	ORcudaSafeCall(cudaMalloc((void**) &noTriangles_device, sizeof(unsigned int)));

	ORcudaSafeCall(cudaMalloc((void**) &indexData_device, sizeof(MultiIndexData)));
	ORcudaSafeCall(cudaMalloc((void**) &voxelData_device, sizeof(MultiVoxelData)));
}

template<class TVoxel>
MultiMeshingEngine_CUDA<TVoxel, VoxelBlockHash>::~MultiMeshingEngine_CUDA() {
	ORcudaSafeCall(cudaFree(visibleBlockGlobalPos_device));
	ORcudaSafeCall(cudaFree(noTriangles_device));

	ORcudaSafeCall(cudaFree(indexData_device));
	ORcudaSafeCall(cudaFree(voxelData_device));
}

template<class TVoxel>
Mesh MultiMeshingEngine_CUDA<TVoxel, VoxelBlockHash>::MeshScene(const VoxelMapGraphManager<TVoxel, VoxelBlockHash>& manager) {
	const VoxelVolumeParameters& sceneParams = manager.getLocalMap(0)->volume->GetParameters();
	int numLocalMaps = (int) manager.numLocalMaps();

	{ // prepare MultiIndex etc.
		if (numLocalMaps > MAX_NUM_LOCALMAPS) numLocalMaps = MAX_NUM_LOCALMAPS;
		indexData_host.numLocalMaps = numLocalMaps;
		for (int localMapId = 0; localMapId < numLocalMaps; ++localMapId) {
			indexData_host.poses_vs[localMapId] = manager.getEstimatedGlobalPose(localMapId).GetM();
			indexData_host.poses_vs[localMapId].m30 /= sceneParams.voxel_size;
			indexData_host.poses_vs[localMapId].m31 /= sceneParams.voxel_size;
			indexData_host.poses_vs[localMapId].m32 /= sceneParams.voxel_size;
			indexData_host.posesInv[localMapId] = manager.getEstimatedGlobalPose(localMapId).GetInvM();
			indexData_host.posesInv[localMapId].m30 /= sceneParams.voxel_size;
			indexData_host.posesInv[localMapId].m31 /= sceneParams.voxel_size;
			indexData_host.posesInv[localMapId].m32 /= sceneParams.voxel_size;
			indexData_host.index[localMapId] = manager.getLocalMap(localMapId)->volume->index.GetIndexData();
			voxelData_host.voxels[localMapId] = manager.getLocalMap(localMapId)->volume->GetVoxels();
		}

		ORcudaSafeCall(cudaMemcpy(indexData_device, &(indexData_host), sizeof(MultiIndexData), cudaMemcpyHostToDevice));
		ORcudaSafeCall(cudaMemcpy(voxelData_device, &(voxelData_host), sizeof(MultiVoxelData), cudaMemcpyHostToDevice));
	}
	const int noMaxTriangles = manager.getLocalMap(0)->volume->index.GetMaxVoxelCount();
	ORUtils::MemoryBlock<Mesh::Triangle> triangles(noMaxTriangles, MEMORYDEVICE_CUDA);
	Mesh::Triangle* triangles_device = triangles.GetData(MEMORYDEVICE_CUDA);

	typedef MultiVoxel<TVoxel> VD;
	typedef MultiIndex<VoxelBlockHash> ID;


	const int hashEntryCount = manager.getLocalMap(0)->volume->index.hash_entry_count;
	const unsigned int voxelBlockCount = manager.getLocalMap(0)->volume->index.voxel_block_count;
	float factor = sceneParams.voxel_size;
	ORcudaSafeCall(cudaMemset(noTriangles_device, 0, sizeof(unsigned int)));
	ORcudaSafeCall(cudaMemset(visibleBlockGlobalPos_device, 0, sizeof(Vector4s) * voxelBlockCount));

	{ // identify used voxel blocks
		dim3 cudaBlockSize(256);
		dim3 gridSize((int) ceil((float) hashEntryCount / (float) cudaBlockSize.x), numLocalMaps);

		findAllocateBlocks<typename ID::IndexData> <<< gridSize, cudaBlockSize >>>
		                                                          (visibleBlockGlobalPos_device, indexData_device, hashEntryCount, voxelBlockCount);
		ORcudaKernelCheck;
	}
	unsigned int triangle_count;
	{ // mesh used voxel blocks
		dim3 cudaBlockSize(VOXEL_BLOCK_SIZE, VOXEL_BLOCK_SIZE, VOXEL_BLOCK_SIZE);
		dim3 gridSize(voxelBlockCount / 16, 16, numLocalMaps);

		meshScene_device<VD, typename ID::IndexData>
				<<< gridSize, cudaBlockSize >>>
		                       (triangles_device, noTriangles_device, factor,
		                        voxelBlockCount, noMaxTriangles,
		                        visibleBlockGlobalPos_device, voxelData_device, indexData_device);
		ORcudaKernelCheck;

		ORcudaSafeCall(
				cudaMemcpy(&triangle_count, noTriangles_device, sizeof(unsigned int), cudaMemcpyDeviceToHost));

	}
	return Mesh(triangles, ORUTILS_MIN(triangle_count, noMaxTriangles));
}

template<class TMultiIndex>
__global__ void findAllocateBlocks(Vector4s* visibleBlockGlobalPos, const TMultiIndex* hashTables,
                                   const int hashEntryCount, const unsigned int voxelBlockCount) {
	int entryId = threadIdx.x + blockIdx.x * blockDim.x;
	if (entryId >= hashEntryCount) return;

	HashEntry* hashTable = hashTables->index[blockIdx.y];

	const HashEntry& currentHashEntry = hashTable[entryId];

	if (currentHashEntry.ptr >= 0)
		visibleBlockGlobalPos[currentHashEntry.ptr + blockIdx.y * voxelBlockCount] = Vector4s(currentHashEntry.pos.x,
		                                                                                      currentHashEntry.pos.y,
		                                                                                      currentHashEntry.pos.z,
		                                                                                      1);
}

template<class TMultiVoxel, class TMultiIndex>
__global__ void
meshScene_device(Mesh::Triangle* triangles, unsigned int* noTriangles_device, float factor,
                 const unsigned int voxelBlockCount, int noMaxTriangles, const Vector4s* visibleBlockGlobalPos,
                 const TMultiVoxel* localVBAs, const TMultiIndex* hashTables) {

	const Vector4s globalPos_4s = visibleBlockGlobalPos[blockIdx.x + gridDim.x * blockIdx.y +
	                                                    blockIdx.z * voxelBlockCount];

	if (globalPos_4s.w == 0) return;

	Vector3i globalPos = Vector3i(globalPos_4s.x, globalPos_4s.y, globalPos_4s.z) * VOXEL_BLOCK_SIZE;

	Vector3f vertList[12];
	int cubeIndex = buildVertListMulti(vertList, globalPos, Vector3i(threadIdx.x, threadIdx.y, threadIdx.z), localVBAs,
	                                   hashTables, blockIdx.z);

	if (cubeIndex < 0) return;

	for (int i = 0; triangle_table[cubeIndex][i] != -1; i += 3) {
		int triangleId = atomicAdd(noTriangles_device, 1);

		if (triangleId < noMaxTriangles - 1) {
			triangles[triangleId].p0 = vertList[triangle_table[cubeIndex][i]] * factor;
			triangles[triangleId].p1 = vertList[triangle_table[cubeIndex][i + 1]] * factor;
			triangles[triangleId].p2 = vertList[triangle_table[cubeIndex][i + 2]] * factor;
		}
	}
}