//  ================================================================
//  Created by Gregory Kramida on 7/24/18.
//  Copyright (c) 2018-2025 Gregory Kramida
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at

//  http://www.apache.org/licenses/LICENSE-2.0

//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//  ================================================================

#include "ITMSceneManipulationEngine_CUDA.h"
#include "../Shared/ITMSceneManipulationEngine_Shared.h"
#include "../../../../ORUtils/PlatformIndependence.h"
#include "../../../Utils/ITMCUDAUtils.h"
#include "../../../../ORUtils/JetbrainsCUDASyntax.hpp"
#include "../../../Objects/Scene/ITMRepresentationAccess.h"

#include <cstring>
#include <iostream>

using namespace ITMLib;

namespace {

// device functions

template<class TVoxel>
__global__ void setVoxel_device(TVoxel* voxelArray, const ITMPlainVoxelArray::ITMVoxelArrayInfo* arrayInfo,
                                const Vector3i location, TVoxel value) {
	int vmIndex;
	int linearIndex = findVoxel(arrayInfo, location, vmIndex);

	if (linearIndex > -1) {
		voxelArray[linearIndex] = value;
	}
}

template<class TVoxel>
__global__ void readVoxel_device(TVoxel* voxelArray, const ITMPlainVoxelArray::ITMVoxelArrayInfo* arrayInfo,
                                 const Vector3i at, ReadVoxelResult<TVoxel>* result) {
	int vmIndex = 0;
	int arrayIndex = findVoxel(arrayInfo, at, vmIndex);
	if (arrayIndex < 0) {
		result->found = false;
	} else {
		result->found = true;
		result->voxel = voxelArray[arrayIndex];
	}
}

__global__ void isPointInBounds_device(
		const ITMPlainVoxelArray::ITMVoxelArrayInfo* index_bounds,
		const Vector3i at, bool* answer) {
	Vector3i point2 = at - index_bounds->offset;
	*answer = !((point2.x < 0) || (point2.x >= index_bounds->size.x) ||
	            (point2.y < 0) || (point2.y >= index_bounds->size.y) ||
	            (point2.z < 0) || (point2.z >= index_bounds->size.z));

}

template<class TVoxel>
__global__ void directCopy_device(TVoxel* destinationArray, const TVoxel* sourceArray,
                                  const ITMPlainVoxelArray::ITMVoxelArrayInfo* arrayInfo) {
	int x = blockIdx.x * blockDim.x + threadIdx.x;
	int y = blockIdx.y * blockDim.y + threadIdx.y;
	int z = blockIdx.z * blockDim.z + threadIdx.z;
	int locId;
	locId = x + y * arrayInfo->size.x + z * arrayInfo->size.x * arrayInfo->size.y;
	destinationArray[locId] = sourceArray[locId];
}

template<class TVoxel>
__global__ void offsetCopy_device(TVoxel* destinationArray, const TVoxel* sourceArray,
                                  const ITMPlainVoxelArray::ITMVoxelArrayInfo* destinationIndexData,
                                  const ITMPlainVoxelArray::ITMVoxelArrayInfo* sourceIndexData,
                                  const Vector3i offset, const Vector3i minPointSourceSansOffset,
                                  const Vector3i extent) {
	int x = blockIdx.x * blockDim.x + threadIdx.x;
	int y = blockIdx.y * blockDim.y + threadIdx.y;
	int z = blockIdx.z * blockDim.z + threadIdx.z;
	if(x >= extent.x || y >= extent.y || z >= extent.z ) return;
	x += minPointSourceSansOffset.x;
	y += minPointSourceSansOffset.y;
	z += minPointSourceSansOffset.z;
	int linearSourceIndex = x + y * sourceIndexData->size.x + z * sourceIndexData->size.x * sourceIndexData->size.y;
	int destination_z, destination_y, destination_x;
	destination_z = z + sourceIndexData->offset.z - destinationIndexData->offset.z + offset.z;
	destination_y = y + sourceIndexData->offset.y - destinationIndexData->offset.y + offset.y;
	destination_x = x + sourceIndexData->offset.x - destinationIndexData->offset.x + offset.x;

	int linearDestinationIndex = destination_x + destination_y * destinationIndexData->size.x +
	                             destination_z * destinationIndexData->size.x * destinationIndexData->size.y;
	destinationArray[linearDestinationIndex] = sourceArray[linearSourceIndex];
}

} // anonymous namespace (device functions)

// region ================================== PLAIN VOXEL ARRAY =========================================================


template<typename TVoxel>
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::ITMSceneManipulationEngine_CUDA() {
	ORcudaSafeCall(cudaMalloc((void**) &readVoxelResult_device, sizeof(ReadVoxelResult<TVoxel>)));
	ORcudaSafeCall(cudaMallocHost((void**) &readVoxelResult_host, sizeof(ReadVoxelResult<TVoxel>)));
}

template<typename TVoxel>
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::~ITMSceneManipulationEngine_CUDA() {
	ORcudaSafeCall(cudaFree(readVoxelResult_device));
	ORcudaSafeCall(cudaFreeHost(readVoxelResult_host));
}

template<typename TVoxel>
void
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::ResetScene(
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* scene) {
	int numBlocks = scene->index.GetAllocatedBlockCount();
	int blockSize = scene->index.GetVoxelBlockSize();

	TVoxel* voxelBlocks_ptr = scene->localVBA.GetVoxelBlocks();
	memsetKernel<TVoxel>(voxelBlocks_ptr, TVoxel(), numBlocks * blockSize);
	ORcudaKernelCheck;
	int* vbaAllocationList_ptr = scene->localVBA.GetAllocationList();
	fillArrayKernel<int>(vbaAllocationList_ptr, numBlocks);
	scene->localVBA.lastFreeBlockId = numBlocks - 1;
}

template<typename TVoxel>
bool
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::SetVoxel(ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* scene,
                                                                      Vector3i at, TVoxel voxel) {
	TVoxel* localVBA = scene->localVBA.GetVoxelBlocks();
	const ITMPlainVoxelArray::ITMVoxelArrayInfo* arrayInfo = scene->index.GetIndexData();
	setVoxel_device<TVoxel> << < 1, 1 >> > (localVBA, arrayInfo, at, voxel);
	ORcudaKernelCheck;
	return true;
}

template<typename TVoxel>
TVoxel
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::ReadVoxel(
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* scene,
		Vector3i at) {

	TVoxel* localVBA = scene->localVBA.GetVoxelBlocks();
	const ITMPlainVoxelArray::ITMVoxelArrayInfo* arrayInfo = scene->index.GetIndexData();

	readVoxel_device<TVoxel> << < 1, 1 >> >
	                                 (localVBA, arrayInfo, at, (ReadVoxelResult<TVoxel>*) readVoxelResult_device);
	ORcudaKernelCheck;

	ReadVoxelResult<TVoxel>* readVoxelResult = (ReadVoxelResult<TVoxel>*) readVoxelResult_host;
	ORcudaSafeCall(cudaMemcpy(readVoxelResult, readVoxelResult_device, sizeof(ReadVoxelResult<TVoxel>),
	                          cudaMemcpyDeviceToHost));

	if (readVoxelResult->found) {
		return readVoxelResult->voxel;
	} else {
		TVoxel voxel;
		return voxel;
	}
}

template<typename TVoxel>
TVoxel
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::ReadVoxel(
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* scene,
		Vector3i at,
		ITMPlainVoxelArray::IndexCache& cache) {
	return ReadVoxel(scene, at);
}

template<typename TVoxel>
void
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::OffsetWarps(
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* scene,
		Vector3f offset) {
	DIEWITHEXCEPTION_REPORTLOCATION("Not implemented");
}

template<typename TVoxel>
bool
ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::IsPointInBounds(
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* scene, const Vector3i& at) {
	bool* in_bounds_CUDA;
	ORcudaSafeCall(cudaMalloc((void**) &in_bounds_CUDA, sizeof(bool)));
	bool in_bounds;
	const ITMPlainVoxelArray::ITMVoxelArrayInfo* arrayInfo = scene->index.GetIndexData();
	isPointInBounds_device << < 1, 1 >> > (arrayInfo, at, in_bounds_CUDA);
	ORcudaKernelCheck;
	ORcudaSafeCall(cudaMemcpy(&in_bounds, in_bounds_CUDA, sizeof(bool), cudaMemcpyDeviceToHost));
	ORcudaSafeCall(cudaFree(in_bounds_CUDA));
	return in_bounds;
}

template<typename TVoxel>
bool ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::CopySceneSlice(
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* destination, ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* source,
		Vector6i bounds, const Vector3i& offset) {
	Vector3i min_pt_source = Vector3i(bounds.min_x, bounds.min_y, bounds.min_z);
	Vector3i max_pt_source = Vector3i(bounds.max_x-1, bounds.max_y - 1, bounds.max_z - 1);

	if (!ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::IsPointInBounds(source, min_pt_source) ||
	    !ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::IsPointInBounds(source, max_pt_source)) {
		DIEWITHEXCEPTION_REPORTLOCATION(
				"Specified source volume is at least partially out of bounds of the source scene.");
	}

	Vector6i bounds_destination;

	bounds_destination.min_x = bounds.min_x + offset.x;
	bounds_destination.max_x = bounds.max_x + offset.x;
	bounds_destination.min_y = bounds.min_y + offset.y;
	bounds_destination.max_y = bounds.max_y + offset.y;
	bounds_destination.min_z = bounds.min_z + offset.z;
	bounds_destination.max_z = bounds.max_z + offset.z;

	Vector3i min_pt_destination = Vector3i(bounds_destination.min_x, bounds_destination.min_y,
	                                       bounds_destination.min_z);
	Vector3i max_pt_destination = Vector3i(bounds_destination.max_x - 1, bounds_destination.max_y - 1,
	                                       bounds_destination.max_z - 1);

	if (!ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::IsPointInBounds(destination,
	                                                                                  min_pt_destination) ||
	    !ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::IsPointInBounds(destination,
	                                                                                  max_pt_destination)) {
		DIEWITHEXCEPTION_REPORTLOCATION(
				"Targeted volume is at least partially out of bounds of the destination scene.");
	}

	TVoxel* sourceVoxels = source->localVBA.GetVoxelBlocks();
	TVoxel* destinationVoxels = destination->localVBA.GetVoxelBlocks();

	if (offset == Vector3i(0)) {
		const ITMPlainVoxelArray::IndexData* indexData = source->index.GetIndexData();

		dim3 cudaBlockSize(8, 8, 8);
		dim3 gridSize(source->index.GetVolumeSize().x / cudaBlockSize.x,
		              source->index.GetVolumeSize().y / cudaBlockSize.y,
		              source->index.GetVolumeSize().z / cudaBlockSize.z);

		directCopy_device<TVoxel> << < gridSize, cudaBlockSize >> > (destinationVoxels, sourceVoxels, indexData);
		ORcudaKernelCheck;

	} else {
		const ITMPlainVoxelArray::IndexData* sourceIndexData = source->index.GetIndexData();
		const ITMPlainVoxelArray::IndexData* destinationIndexData = destination->index.GetIndexData();

		Vector3i extent = max_pt_source + Vector3i(1,1,1) - min_pt_source;
		Vector3i minSourcePointSansOffset = min_pt_source - source->index.GetVolumeOffset();
		dim3 cudaBlockSize(8, 8, 8);
		//(int) ceil((float) totalHashEntryCount / (float) cudaBlockSizeAL.x)
		dim3 gridSize((int) ceil((float) extent.x / (float) cudaBlockSize.x),
		              (int) ceil((float) extent.y / (float) cudaBlockSize.y),
		              (int) ceil((float) extent.z / (float) cudaBlockSize.z));

		offsetCopy_device<TVoxel> << < gridSize, cudaBlockSize >> >
		                                         (destinationVoxels, sourceVoxels, destinationIndexData,
				                                         sourceIndexData, offset, minSourcePointSansOffset,
				                                         extent);
		ORcudaKernelCheck;

	}
	return true;
}

template<typename TVoxel>
inline static Vector6i GetSceneBounds(ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* source) {
	Vector3i offset = source->index.GetVolumeOffset();
	Vector3i size = source->index.GetVolumeSize();
	return {offset.x, offset.y, offset.z,
	        offset.x + size.x, offset.y + size.y, offset.z + size.z};
}

template<typename TVoxel>
bool ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::CopyScene(
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* destination,
		ITMVoxelVolume<TVoxel, ITMPlainVoxelArray>* source,
		const Vector3i& offset) {
	ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::ResetScene(destination);
	//TODO: this bounds treatment isn't quite correct -- it assumes same bounds for source & dest. Need to fix.
	Vector6i bounds = GetSceneBounds(source);
	if (offset.x > 0) {
		bounds.max_x -= offset.x;
	} else {
		bounds.min_x -= offset.x;
	}
	if (offset.y > 0) {
		bounds.max_y -= offset.y;
	} else {
		bounds.min_y -= offset.y;
	}
	if (offset.z > 0) {
		bounds.max_z -= offset.z;
	} else {
		bounds.min_z -= offset.z;
	}
	return ITMSceneManipulationEngine_CUDA<TVoxel, ITMPlainVoxelArray>::
	CopySceneSlice(destination, source, bounds, offset);
}


//endregion ============================================================================================================